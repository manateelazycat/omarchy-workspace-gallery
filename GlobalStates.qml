import "."
import QtQuick
import Quickshell
import Quickshell.Hyprland
import Quickshell.Io
import Quickshell.Hyprland._GlobalShortcuts 0.0
pragma Singleton
pragma ComponentBehavior: Bound

Singleton {
    id: root
    signal gallerySwipeStarted(real deltaX, real timestamp)
    signal gallerySwipeUpdated(real deltaX, real timestamp)
    signal gallerySwipeFinished(bool cancelled, real timestamp)
    signal galleryStepRequested(int delta)

    property bool barOpen: true
    property bool clipboardOpen: false
    property bool osdBrightnessOpen: false
    property bool osdVolumeOpen: false
    property bool osdInputMethodOpen: false
    property real osdBrightnessValue: -1
    // Monitor name the brightness OSD should pin to (empty = focused screen).
    property string osdBrightnessScreen: ""
    property bool overviewOpen: false
    // Armed by Ctrl+Shift+X inside overview. In this mode a left click kills
    // the selected client instead of focusing or dragging it.
    property bool overviewKillMode: false
    property string overviewAnchorMonitorName: ""
    property bool overviewSearchMode: false
    // The plugin's optimized visual ordering is the default. The persisted
    // setting can still switch to the native Omarchy order explicitly.
    property string overviewSortMode: "system"
    // When true, h/j/k/l keep navigating workspaces and search opens on "/" --
    // the vim idiom. When false any printable character opens search, which is
    // quicker but takes those keys away from navigation.
    property bool overviewVimKeys: true
    // Each overlay draws only its own monitor's workspaces. Persisted to the bar
    // widget entry in shell.json, the same way overviewSortMode is.
    property bool overviewPerMonitor: true
    property int overviewFocusedWorkspaceId: -1
    property var overviewWorkspaceMru: []
    property int overviewCurrentWorkspaceId: -1
    property int overviewPreviousWorkspaceId: -1
    property int overviewDraggingFromWorkspace: -1
    property int overviewDraggingTargetWorkspace: -1
    property bool overviewDraggingTargetIsTrailing: false
    property string overviewDraggingTargetMonitor: ""
    property var overviewSuppressedEmptyWorkspaceIds: []
    property var overviewPendingWorkspaceMonitorById: ({})
    property var overviewPendingOccupiedWorkspaces: []
    property var overviewPendingWindowWorkspaceByAddress: ({})
    // Compaction is presented in two phases: cards first gather visually, then
    // their real Hyprland workspace moves are committed behind a short fade.
    property bool overviewCompactionAnimating: false
    property bool overviewCompactionSyncing: false
    property bool overviewCompactionHandoff: false
    property var overviewCompactionMoves: []
    property var overviewCompactionTimeline: ({ emptyStages: [], shiftStages: [], duration: 0 })
    property real overviewCompactionElapsed: 0
    property int overviewRefreshSerial: 0
    property bool regionSelectorOpen: false
    property bool screenshotActive: false
    property bool screenLocked: false
    property bool screenLockContainsCharacters: false
    property bool screenUnlockFailed: false
    property bool superDown: false
    property bool superReleaseMightTrigger: false
    // Open Overview on Super-down for a responsive standalone key press. If
    // another key arrives, the input listener closes this speculative open
    // before the chord's own binding handles it.
    property bool overviewOpenedBySuperDown: false
    // The overview process is pre-warmed separately from the bar. During its
    // short startup window, a compositor-delivered Super release must not be
    // mistaken for a user request to open Overview.
    property bool overviewWarmStart: Quickshell.env("SUMIKA_OVERVIEW_WARM") === "1"
    // Overview controller, injected by the overview module at load time so the
    // Super-release shortcut can drive switching mode without a core→module
    // import dependency. Null in processes that don't load the overview module.
    property var overviewSwitchingController: null
    property string barPopupType: ""
    // Screen name of the bar that opened the popup (multi-monitor: pin panel + brightness).
    property string barPopupAnchorScreen: ""
    // Ephemeral popups (e.g. volume OSD) auto-close; pinned ones stay until dismissed.
    property bool barPopupEphemeral: false
    property real barPopupDismissedAt: 0
    property bool sessionConfirmOpen: false
    property string sessionConfirmAction: ""
    // Label shown in the confirm dialog (set by requestSessionConfirm).
    property string sessionConfirmLabel: ""
    // Shared "save session on exit" preference (default on). The BarStatusPopup
    // checkbox binds to this; the right-click PowerContextMenu reads it so both
    // entry points honor the same choice.
    property bool sessionSaveOnExit: true

    Timer {
        id: overviewWarmStartTimer
        interval: 3000
        running: root.overviewWarmStart
        onTriggered: root.overviewWarmStart = false
    }

    function requestSessionConfirm(action, label) {
        GlobalStates.barPopupType = "";
        GlobalStates.sessionConfirmAction = action;
        GlobalStates.sessionConfirmLabel = label;
        GlobalStates.sessionConfirmOpen = true;
    }

    function closeSessionConfirm() {
        GlobalStates.sessionConfirmOpen = false;
        GlobalStates.sessionConfirmAction = "";
        GlobalStates.sessionConfirmLabel = "";
    }

    onOverviewOpenChanged: {
        if (GlobalStates.overviewOpen) {
            GlobalStates.clipboardOpen = false;
            GlobalStates.overviewSearchMode = false;
        }
    }

    // MRU (Most Recently Used) workspace list, mirroring Win11 Alt+Tab Z-order.
    // Promote `wsId` to the front of the list (Win11: switched window → top of Z-order).
    // The trailing "New workspace" slot never enters MRU — it is always last.
    function promoteWorkspaceMru(wsId) {
        if (wsId < 1)
            return;
        if (GlobalStates.overviewWorkspaceMru.length > 0
                && GlobalStates.overviewWorkspaceMru[0] === wsId)
            return;
        const next = GlobalStates.overviewWorkspaceMru.filter(id => id !== wsId);
        next.unshift(wsId);
        GlobalStates.overviewWorkspaceMru = next;
    }

    function observeWorkspaceHistory(wsId) {
        const id = Number(wsId);
        if (id < 1 || id > 100 || id === GlobalStates.overviewCurrentWorkspaceId)
            return;
        if (GlobalStates.overviewCurrentWorkspaceId > 0)
            GlobalStates.overviewPreviousWorkspaceId = GlobalStates.overviewCurrentWorkspaceId;
        GlobalStates.overviewCurrentWorkspaceId = id;
    }

    function refreshOverviewModel() {
        GlobalStates.overviewRefreshSerial += 1;
    }

    function setPendingWindowWorkspace(address, workspaceId) {
        const key = String(address ?? "");
        if (key.length === 0)
            return;
        const next = Object.assign({}, root.overviewPendingWindowWorkspaceByAddress ?? {});
        if (workspaceId > 0)
            next[key] = workspaceId;
        else
            delete next[key];
        root.overviewPendingWindowWorkspaceByAddress = next;
    }

    function suppressEmptyWorkspace(wsId) {
        if (wsId < 1)
            return;
        const current = GlobalStates.overviewSuppressedEmptyWorkspaceIds ?? [];
        if (current.includes(wsId))
            return;
        const next = current.slice();
        next.push(wsId);
        GlobalStates.overviewSuppressedEmptyWorkspaceIds = next;
    }

    function unsuppressWorkspace(wsId) {
        if (wsId < 1)
            return;
        GlobalStates.overviewSuppressedEmptyWorkspaceIds =
            (GlobalStates.overviewSuppressedEmptyWorkspaceIds ?? []).filter(id => id !== wsId);
    }

    onBarPopupTypeChanged: {
        if (!GlobalStates.barPopupType)
            GlobalStates.barPopupEphemeral = false;
    }

    Connections {
        target: Hyprland
        function onFocusedWorkspaceChanged() {
            root.observeWorkspaceHistory(Hyprland.focusedWorkspace?.id ?? 0);
        }
    }

    Component.onCompleted: root.observeWorkspaceHistory(Hyprland.focusedWorkspace?.id ?? 0)
}
